//https://goguardian--goguardian.lightning.force.com/lightning/r/Account/0014N0000233xj3QAA/view




// 1.Create a new class that implements Batch apex.
global class BatchRenewalOpp implements Database.Batchable<SObject>, Database.Stateful {
    
    
    global Set<Id> oppIds = new Set<Id>();
    global Map<Id, Set<sObject>> multipleRenewOpps = new Map<Id, Set<sObject>>(); 
    global Integer daysBeforeRenew;
    
    global BatchRenewalOpp(Integer daysBeforeRenew){
        this.daysBeforeRenew = daysBeforeRenew;
    }
      
    
    // 2.Declare the start method to return a list of accounts.  			
    // N should be formula field to reference # of days before the contract expiration that we want to create the opportunity
    global Database.QueryLocator start(Database.BatchableContext bc) {
        Date dte = System.today() + daysBeforeRenew;
        system.debug(dte);
        
        String query = Test.isRunningTest() ? 'SELECT ID, Name, Most_Recent_AP_Contact__c, OwnerId, Contract_End_Date_Count__c, Max_Contract_End__c, CSM__c,'+
             'BillingStreet, BillingCity, BillingState, Most_Recent_Channel__c,'+
             'Most_Recent_Reseller__c, Most_Recent_Reseller_Contact__c, Most_Recent_Admin__c FROM Account LIMIT 1' : 'SELECT ID, Name, Most_Recent_AP_Contact__c, OwnerId, Contract_End_Date_Count__c, Max_Contract_End__c, CSM__c,'+
             'BillingStreet, BillingCity, BillingState, Most_Recent_Channel__c,'+
             'Most_Recent_Reseller__c, Most_Recent_Reseller_Contact__c, Most_Recent_Admin__c FROM Account '+
             'WHERE Contract_End_Date_Count__c = 1 AND Max_Contract_End__c = :dte';
                
        return Database.getQueryLocator(query);

        
    }
    
    global Opportunity createRenewalOpp(Account acct, Id contractId){
        //add fields in PRODUCTION: ImpartnerPRM__PartnerContact__c / ImpartnerPRM__PartnerAccount__c
        
        //Hola amigos. For the CSM renewal creation, can we make sure that these two fields carry over from the original opp if they have values. These are the channel partner fields from Impartner for deal reg deals. We aren't going to populate the checkbox that would sync these back to Impartner, just these two fields.
        //ImpartnerPRM__PartnerContact__c
    	//ImpartnerPRM__PartnerAccount__c
        
        
        String channel = acct.Most_Recent_Channel__c;
        Opportunity opp = new Opportunity();
        Date closeDate = System.today() + daysBeforeRenew;
        opp.Name = String.valueOf(closeDate.year()) + ' | ' + acct.Name + ' | Renewal Opportunity';
        opp.Type = 'Renewal';
        opp.CloseDate = closeDate;
        opp.SBQQ__RenewedContract__c = contractId;
        opp.AccountId = acct.Id;
        opp.StageName = 'Defined';
        opp.OwnerId = acct.CSM__c == Null ? acct.OwnerId : acct.CSM__c;
        opp.Admin_Contact__c =acct.Most_Recent_Admin__c;
        opp.Channel__c = channel;
        if(channel == 'Direct'){
            opp.AP_Contact__c = acct.Most_Recent_AP_Contact__c;
        }
        if(channel == 'Indirect'){
            opp.QuotePrimaryPartner__c = acct.Most_Recent_Reseller__c;
            opp.ImpartnerPRM__PartnerAccount__c = acct.Most_Recent_Reseller__c;
            opp.ImpartnerPRM__PartnerContact__c = acct.Most_Recent_Reseller_Contact__c;
            opp.Partner_Rep__c = acct.Most_Recent_Reseller_Contact__c;
        }
        
        return opp;
    }
    
    // 4.Declare the execute method to return void and take in a BatchableContext as well as a list of accounts.
    global void execute(Database.BatchableContext bc, List<Account> scope){
        
        //List of Ids to pass to BatchRenewalQuote

        system.debug('Scope: ' + scope);
      
        // 6.Create a Map of Account pulling out the Id as a key
        List<Contract> updateContracts = new List<Contract>();
        Map <ID, Account> accountMap = new Map <ID, Account>(scope);
        List <Opportunity> renewalOppList = new List<Opportunity>();
        // 7.Query for contracts where do_not_renew__c = false and end date is today + n days and AccountId is in the account map key set.
        Date dte = System.today() + daysBeforeRenew;
        List<Contract> contracts = [SELECT ID, ContractNumber, SBQQ__RenewalOpportunity__c, EndDate, AccountId, Account.OwnerId 
                                    FROM Contract 
                                    WHERE Do_Not_Renew__c = FALSE AND AccountId IN :accountMap.keySet() AND EndDate = :dte];
        
        Map<Id, List<Contract>> acctContractMap = new Map<Id, List<Contract>>();
        Map<Id, Set<Id>> acctOppIdMap = new Map<Id, Set<Id>>();
        
        system.debug(acctContractMap);
        system.debug(acctOppIdMap);
        
        for(Contract cont : contracts){
            Id acctId = cont.AccountId;
            Set<Id> renewalOppIds = acctOppIdMap.containsKey(acctId) ? acctOppIdMap.get(acctId) : new Set<Id>();
            List<Contract> conts = acctContractMap.containsKey(acctId) ? acctContractMap.get(acctId) : new List<Contract>();
            if(cont.SBQQ__RenewalOpportunity__c != Null){
                renewalOppIds.add(cont.SBQQ__RenewalOpportunity__c);
            }
            conts.add(cont);
            
            acctContractMap.put(acctId,conts);
            acctOppIdMap.put(acctId,renewalOppIds);
        }
        
        // There are 3 groups of accounts/contracts
        // A. No pre-existing renewal opps - create one
        // B. One pre-existing renewal opp - give it to the others
        // C. Multiple pre-existing renewal opps - flag it
        // if there is a renewal opp on a contract, take its id and put it on the other contracts for the account??????
        
        for (Id accountId : acctContractMap.keySet()) {
            List<Contract> acctContracts = acctContractMap.get(accountId);
            Set<Id> renewalOppIds = acctOppIdMap.get(accountId);
            Integer numOpps = renewalOppIds.size();
            
            switch on numOpps{
                when 0 {
                    Account acct = accountMap.get(accountId);
                    Contract cont = acctContracts[0];
                    Opportunity renewalOpp = createRenewalOpp(acct, cont.Id);
                    renewalOppList.add(renewalOpp);
                }
                when 1 {
                    Id oppId = (new List<Id>(renewalOppIds))[0];
                    //add existing reenwal oppId to oppIds list
                    //oppIds.add(oppId);
                    for(Contract cont : acctContracts){
                        if(cont.SBQQ__RenewalOpportunity__c != oppId){
                            cont.SBQQ__RenewalOpportunity__c = oppId;
                            updateContracts.add(cont);
                        }
                    }
                }
                when else{
                    Account acct = accountMap.get(accountId);
                    Id ownerId = acct.OwnerId;
                    Set<sObject> acctSet = multipleRenewOpps.containsKey(ownerId) ? multipleRenewOpps.get(ownerId) : new Set<sObject>();
                    acctSet.add(acct);
                    multipleRenewOpps.put(ownerId,acctSet);
                }
            }
        }
        
        if(renewalOppList.size() > 0){
            insert renewalOppList;
            for(Opportunity opp : renewalOppList){
                //adding new opp id and adding it to global set oppIds
                oppIds.add(opp.Id);
                Id acctId = opp.AccountId;
                List<Contract> contractList = acctContractMap.get(acctId);
                for(Contract cont : contractList){
                    cont.SBQQ__RenewalOpportunity__c = opp.Id;
                    updateContracts.add(cont);
                }
            }
        }
        
        if(updateContracts.size() > 0){
            update updateContracts;
        }
        
    }
    
    // 13. Finish method will call Database.executeBatch(new RenewalQuoteGenerator());
    global void finish(Database.BatchableContext bc){
        
        // handle multiple opps for one account
        // use State to track
        if (!multipleRenewOpps.isEmpty()){
            List <Messaging.SingleEmailMessage> messages = new List <Messaging.SingleEmailMessage>();
            Map<Id, String> ownerHtmlMap = EmailHelper.setRecordLinkHtml(multipleRenewOpps, 'Multiple_Renew_Opps');
            for (id ownerId : ownerHtmlMap.keySet()) {
                Messaging.SingleEmailMessage msg =  new Messaging.SingleEmailMessage();
                msg.setTargetObjectId(ownerId);
                msg.setSaveAsActivity(False);
                msg.setCcAddresses(new List<String>{'salesops@goguardian.com'});
                msg.setHtmlBody(ownerHtmlMap.get(ownerId));
                messages.add(msg);     
            }
            
            messaging.sendEmail(messages);
            
        }
        
        BatchRenewalQuote job = new BatchRenewalQuote(new List<Id>(oppIds), daysBeforeRenew);
        Database.executeBatch(job, 1);
    
		
        
        
        
        // Get the ID of the AsyncApexJob representing this batch job
        // from Database.BatchableContext.
        // Query the AsyncApexJob object to retrieve the current job's information.
        AsyncApexJob a = [SELECT Id, Status, NumberOfErrors, JobItemsProcessed,
                          TotalJobItems, CreatedBy.Email
                          FROM AsyncApexJob WHERE Id =
                          :bc.getJobId()];
        // Send an email to the Apex job's submitter notifying of job completion.
        Messaging.SingleEmailMessage mail = new Messaging.SingleEmailMessage();
        String[] toAddresses = new String[] {a.CreatedBy.Email};
            mail.setToAddresses(toAddresses);
        mail.setSubject('Apex Sharing Recalculation ' + a.Status);
        mail.setPlainTextBody
            ('The batch Apex job processed ' + a.TotalJobItems +
             ' batches with '+ a.NumberOfErrors + ' failures.');
        Messaging.sendEmail(new Messaging.SingleEmailMessage[] { mail });
    }
}
